allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = "../build"

subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}

subprojects { subproject ->
    afterEvaluate {
        if (subproject.hasProperty('android')) {
            subproject.android {
                if (namespace == null) {
                    def manifestFile = subproject.file("src/main/AndroidManifest.xml")
                    if (manifestFile.exists()) {
                        def manifest = new XmlSlurper().parse(manifestFile)
                        def packageName = manifest.'@package'.text()
                        if (packageName) {
                            namespace = packageName
                        } else {
                            namespace = "com.google.${subproject.name.replaceAll('_', '.').replaceAll('-', '.')}"
                        }
                    } else {
                        namespace = "com.google.${subproject.name.replaceAll('_', '.').replaceAll('-', '.')}"
                    }
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}